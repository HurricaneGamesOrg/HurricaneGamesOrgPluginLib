plugins {
	id 'java'
	id 'maven-publish'
}


defaultTasks 'build'


group 'org.hurricanegames'
version '1.5.2'


import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.Files


Path projectDirectoryPath = projectDir.toPath().toAbsolutePath()


sourceCompatibility = JavaVersion.VERSION_1_8

sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
	test {
		java {
			srcDirs = ['tests']
		}
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/public/'
	}
	maven {
		url 'https://papermc.io/repo/repository/maven-public/'
	}
}

configurations {
	runtimeDependencies
	compileOnly {
		extendsFrom runtimeDependencies
	}
	testImplementation {
	    extendsFrom runtimeDependencies
	}
}

dependencies {
	runtimeDependencies group: 'com.destroystokyo.paper', name: 'paper-api', version: '1.16.5-R0.1-SNAPSHOT'

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.2.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.2.0'
}

compileJava {
	options.encoding = 'UTF-8'
	options.incremental = false
}


test {
	Path workingDirectoryPath = projectDirectoryPath.resolve('testsRun');

	doFirst {
		classpath -= configurations.runtimeDependencies
		classpath += configurations.runtimeDependencies

		Files.createDirectories(workingDirectoryPath)
	}

	useJUnitPlatform()

	workingDir = workingDirectoryPath;
}


jar {
	from sourceSets.main.java.srcDirs
}


compileJava.finalizedBy(test)



tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

publishing {
	publications {
		release(MavenPublication) {
			from components.java

			artifacts = [jar]
		}
	}
}
